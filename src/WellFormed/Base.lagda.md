---
title: å½¢å¼åŒ–å¤§æ•°æ•°å­¦ (2.0 - è‰¯æ„æ ‘åºæ•°)
zhihu-tags: Agda, å¤§æ•°æ•°å­¦, åºæ•°
---

# å½¢å¼åŒ–å¤§æ•°æ•°å­¦ (2.0 - è‰¯æ„æ ‘åºæ•°)

> äº¤æµQç¾¤: 893531731  
> æœ¬æ–‡æºç : [Base.lagda.md](https://github.com/choukh/agda-googology/blob/main/src/WellFormed/Base.lagda.md)  
> é«˜äº®æ¸²æŸ“: [Base.html](https://choukh.github.io/agda-googology/WellFormed.Base.html)  

## åŸºç¡€çš„é€‰å–

ç«‹æ–¹ç±»å‹è®º

```agda
{-# OPTIONS --safe --cubical #-}
module WellFormed.Base where
```

### åº“ä¾èµ–

cubicalåº“

```agda
open import Cubical.Foundations.Prelude as ğŸ§Š public
  using (Type; toPathP; isProp; isSet; isPropâ†’isSet)
  renaming (_â‰¡_ to Path; refl to reflPath)
open import Cubical.Foundations.HLevels public
open import Cubical.Data.Equality public using (pathToEq; eqToPath; PathPathEq)
open import Cubical.Data.Empty public using (âŠ¥; isPropâŠ¥) renaming (elim to âŠ¥-elim)
open import Cubical.Data.Sigma public using (âˆƒ-syntax; _,_)
open import Cubical.Data.Sum public
  renaming (_âŠ_ to infix 3 _âŠ_) using (inl; inr; isPropâŠ)
open import Cubical.HITs.PropositionalTruncation public
  using (âˆ¥_âˆ¥â‚; âˆ£_âˆ£â‚; squashâ‚) renaming (rec to recâ‚)
```

æ ‡å‡†åº“

```agda
open import Data.Unit public using (âŠ¤; tt)
open import Data.Nat as â„• public using (â„•; zero; suc)
open import Data.Nat.Properties as â„• public using ()
open import Function public using (id; _âˆ˜_; _$_; _âˆ‹_; it; case_of_)
open import Relation.Binary.PropositionalEquality public
  using (_â‰¡_; refl)
```

## è‰¯æ„æ ‘åºæ•°

äº’å½’çº³å®šä¹‰è‰¯æ„æ ‘åºæ•°ä¸å­æ ‘å…³ç³».

```agda
data Ord : Type
Rel = Ord â†’ Ord â†’ Type
data _<_ : Rel; infix 4 _<_
```

```agda
_â‰¤_ : Rel; infix 4 _â‰¤_
a â‰¤ b = a < b âŠ a â‰¡ b
```

**å®šä¹‰** ä¸¥æ ¼å•è°ƒé€’å¢åºåˆ—

```agda
Seq : Type
Seq = â„• â†’ Ord

wf : Seq â†’ Type
wf f = âˆ€ {n} â†’ f n < f (suc n)
```

```agda
variable
  m n : â„•
  a b c d i : Ord
  f g h : Seq
```

**å®šä¹‰** è‰¯æ„æ ‘åºæ•°

```agda
data Ord where
  zero : Ord
  suc  : Ord â†’ Ord
  lim  : (f : Seq) â†’ â¦ƒ wf f â¦„ â†’ Ord
```

**å®šä¹‰** å­æ ‘å…³ç³»

```agda
data _<_ where
  <suc  : a < suc a
  <sucâ‚‚ : a < b â†’ a < suc b
  <lim  : â¦ƒ _ : wf f â¦„ â†’ f n < lim f
  <limâ‚‚ : â¦ƒ _ : wf f â¦„ â†’ a < f n â†’ a < lim f
  isProp< : isProp (a < b)
```

è‰¯æ„æ¡ä»¶æ˜¯å‘½é¢˜

```agda
isPropWf : isProp (wf f)
isPropWf = isPropImplicitÎ  (Î» _ â†’ isProp<)
```

æé™çš„å¤–å»¶æ€§

```agda
limExtğŸ§Š : â¦ƒ _ : wf f â¦„ â¦ƒ _ : wf g â¦„ â†’ (âˆ€ n â†’ Path (f n) (g n)) â†’ Path (lim f) (lim g)
limExtğŸ§Š {f} p = ğŸ§Š.congâ‚‚ (Î» (f : Seq) (wff : wf f) â†’ lim f â¦ƒ wff â¦„)
  (Î» i n â†’ p n i) (toPathP (isPropWf _ _))

limExt : â¦ƒ _ : wf f â¦„ â¦ƒ _ : wf g â¦„ â†’ (âˆ€ n â†’ f n â‰¡ g n) â†’ lim f â‰¡ lim g
limExt p = pathToEq $ limExtğŸ§Š $ eqToPath âˆ˜ p
```

## æ ‘åºæ•°æ˜¯é›†åˆ

æˆ‘ä»¬ä½¿ç”¨ [encode-decode æ–¹æ³•](https://ncatlab.org/nlab/show/encode-decode+method) è¯æ˜ $\text{Ord}$ æ˜¯åŒä¼¦å±‚çº§æ„ä¹‰ä¸‹çš„é›†åˆ. å…·ä½“ç»†èŠ‚è¿™é‡Œä¸å±•å¼€, å¤§è‡´åˆ†ä¸ºä»¥ä¸‹å››æ­¥:

1. å®šä¹‰ `a b : Ord` çš„è¦†å ç©ºé—´ `Cover a b`, å®¹æ˜“è¯æ˜å®ƒæ˜¯ä¸€ä¸ªå‘½é¢˜.

```agda
Cover : Ord â†’ Ord â†’ Type
Cover zero    zero    = âŠ¤
Cover (suc a) (suc b) = Cover a b
Cover (lim f) (lim g) = âˆ€ n â†’ Cover (f n) (g n)
Cover _       _       = âŠ¥

reflCode : (a : Ord) â†’ Cover a a
reflCode zero = tt
reflCode (suc a) = reflCode a
reflCode (lim f) n = reflCode (f n)

isPropCover : âˆ€ a b â†’ isProp (Cover a b)
isPropCover zero zero tt tt = reflPath
isPropCover (suc a) (suc b) = isPropCover a b
isPropCover (lim f) (lim g) = isPropÎ  (Î» n â†’ isPropCover (f n) (g n))
```

2. å°† `a b : Ord` çš„é“è·¯ç©ºé—´ `a â‰¡ b` ç¼–ç ä¸ºè¦†å ç©ºé—´.

```agda
encode : âˆ€ a b â†’ Path a b â†’ Cover a b
encode a b = ğŸ§Š.J (Î» b _ â†’ Cover a b) (reflCode a)

encodeRefl : âˆ€ a â†’ Path (encode a a reflPath) (reflCode a)
encodeRefl a = ğŸ§Š.JRefl (Î» b _ â†’ Cover a b) (reflCode a)
```

3. å°†è¦†å ç©ºé—´è§£ç ä¸ºé“è·¯ç©ºé—´.

```agda
decode : âˆ€ a b â†’ Cover a b â†’ Path a b
decode zero zero _ = reflPath
decode (suc a) (suc b) p = ğŸ§Š.cong suc (decode a b p)
decode (lim f) (lim g) p = limExtğŸ§Š Î» n â†’ decode (f n) (g n) (p n)

decodeRefl : âˆ€ a â†’ Path (decode a a (reflCode a)) reflPath
decodeRefl zero = reflPath
decodeRefl (suc a) i = ğŸ§Š.cong suc (decodeRefl a i)
decodeRefl (lim f) i = ğŸ§Š.congâ‚‚
  (Î» (f : Seq) (wff : wf f) â†’ lim f â¦ƒ wff â¦„)
  (Î» j n â†’ decodeRefl (f n) i j)
  (isSetâ†’SquareP {A = Î» i j â†’ wf (Î» n â†’ decodeRefl (f n) i j)}
    (Î» _ _ â†’ isPropâ†’isSet isPropWf) (toPathP (isPropWf _ _)) reflPath reflPath reflPath i)
```

4. è¯æ˜ç¼–ç ä¸è§£ç äº’é€†, ç»“åˆ `Cover a b` æ˜¯å‘½é¢˜, è¯´æ˜ `Path a b` æ˜¯å‘½é¢˜, ä¹Ÿå³ `Ord` æ˜¯é›†åˆ.

```agda
decodeEncode : âˆ€ a b p â†’ Path (decode a b (encode a b p)) p
decodeEncode a _ = ğŸ§Š.J (Î» b p â†’ Path (decode a b (encode a b p)) p)
  (ğŸ§Š.cong (decode a a) (encodeRefl a) ğŸ§Š.âˆ™ decodeRefl a)
  where open import Cubical.Foundations.Isomorphism

isSetOrd : isSet Ord
isSetOrd a b = isOfHLevelRetract 1 (encode a b) (decode a b) (decodeEncode a b) (isPropCover a b)

isPropâ‰¡ : isProp (a â‰¡ b)
isPropâ‰¡ = ğŸ§Š.subst isProp PathPathEq (isSetOrd _ _)
```

## ä¸€äº›çº¦å®š

**å®šä¹‰** è‡ªç„¶æ•°åˆ°åºæ•°çš„åµŒå…¥ $\text{fin} : â„• â†’ \text{Ord}$

$$
\text{fin}~n := \text{suc}^n~0
$$

å…¶ä¸­åç»§å‡½æ•°çš„ä¸Šæ ‡ $n$ è¡¨ç¤ºè¿­ä»£ $n$ æ¬¡.

```agda
open import Lower public using (_âˆ˜â¿_)
fin : Seq
fin n = (suc âˆ˜â¿ n) zero
```

**çº¦å®š** æ•°å­—å­—é¢é‡æ—¢å¯ä»¥è¡¨ç¤ºè‡ªç„¶æ•°, ä¹Ÿå¯ä»¥è¡¨ç¤ºåºæ•°. Agda ä½¿ç”¨[å­—é¢é‡é‡è½½](https://agda.readthedocs.io/en/v2.6.4.3-r1/language/literal-overloading.html)åŠŸèƒ½å®ç°è¯¥çº¦å®š.

```agda
open import Agda.Builtin.FromNat public
instance
  nNat = Number â„•   âˆ‹ record { Constraint = Î» _ â†’ âŠ¤ ; fromNat = Î» n â†’ n }
  nOrd = Number Ord âˆ‹ record { Constraint = Î» _ â†’ âŠ¤ ; fromNat = Î» n â†’ fin n }
```

**çº¦å®š** æˆ‘ä»¬å°† $\text{suc}~(\text{suc}~a)$ è®°ä½œ $a^{++}$.

```agda
pattern 2+ a = suc (suc a)
```

## å­æ ‘å…³ç³»

```agda
<-trans : a < b â†’ b < c â†’ a < c
<-trans a<b <suc = <sucâ‚‚ a<b
<-trans a<f <lim = <limâ‚‚ a<f
<-trans a<b (<sucâ‚‚ b<c) = <sucâ‚‚ (<-trans a<b b<c)
<-trans a<b (<limâ‚‚ b<f) = <limâ‚‚ (<-trans a<b b<f)
<-trans a<b (isProp< p q i) = isProp< (<-trans a<b p) (<-trans a<b q) i
```

```agda
lim-inv : â¦ƒ _ : wf f â¦„ â†’ a < lim f â†’ âˆƒ[ n âˆˆ â„• ] a < f n
lim-inv <lim   = âˆ£ _ , it âˆ£â‚
lim-inv (<limâ‚‚ a<f) = âˆ£ _ , a<f âˆ£â‚
lim-inv (isProp< p q i) = squashâ‚ (lim-inv p) (lim-inv q) i
```

```agda
<-irrefl : a < b â†’ a â‰¡ b â†’ âŠ¥
<-irrefl {a = zero} (isProp< p q i) refl = isPropâŠ¥ (<-irrefl p refl) (<-irrefl q refl) i
<-irrefl {a = suc a} (<sucâ‚‚ p) refl = <-irrefl (<-trans <suc p) refl
<-irrefl {a = suc a} (isProp< p q i) refl = isPropâŠ¥ (<-irrefl p refl) (<-irrefl q refl) i
<-irrefl {a = lim f} p refl = recâ‚ isPropâŠ¥ (Î» { (n , p) â†’ <-irrefl (<-trans <lim p) refl }) (lim-inv p)
```

```agda
isPropâ‰¤ : isProp (a â‰¤ b)
isPropâ‰¤ = isPropâŠ isProp< isPropâ‰¡ <-irrefl
```

```agda
<sâ†’â‰¤ : a < suc b â†’ a â‰¤ b
<sâ†’â‰¤ <suc = inr refl
<sâ†’â‰¤ (<sucâ‚‚ a<b) = inl a<b
<sâ†’â‰¤ (isProp< p q i) = isPropâ‰¤ (<sâ†’â‰¤ p) (<sâ†’â‰¤ q) i
```
